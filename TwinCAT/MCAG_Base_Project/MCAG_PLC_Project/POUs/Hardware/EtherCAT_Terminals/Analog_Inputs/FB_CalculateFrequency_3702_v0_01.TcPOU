<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.25">
  <POU Name="FB_CalculateFrequency_3702_v0_01" Id="{c78cb51e-c142-4983-bbe4-9ac5ede33751}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CalculateFrequency_3702_v0_01
VAR CONSTANT
	///200 samples/period
	cBufferSize: INT := 1000;
END_VAR
VAR_INPUT
	En: BOOL;
	bCalculate: BOOL;
	aBufferValue: ARRAY[0..(cBuffersize - 1)] OF INT;
	aBufferDcTime: ARRAY[0..(cBuffersize - 1)] OF UDINT;
	///If curve has a DC offset
	nDCOffset: INT := 0;
END_VAR
VAR_OUTPUT
	EnO: BOOL;
	bError: BOOL;
	fActFrequency: LREAL;
END_VAR
VAR
	nIndex: INT;
	nFirstZeroCrossing: INT;
	nLastZeroCrossing: INT;
	rTimeFirst: REAL := 0;
	rTimeLast: REAL := 0;
	rTimeRes: REAL := 0;
	nCrossings: INT := 0;
END_VAR
VAR_TEMP
	rRange: REAL := 0;
	rTimeSpan: REAL := 0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[EnO:=En;
bError:=FALSE;
nCrossings:=0;
IF En AND bCalculate THEN
	//Serach for crossings of nDCOffset
	FOR nIndex:=1 TO cBuffersize-1 DO
		IF((aBufferValue[nIndex]>nDCOffset AND aBufferValue[nIndex-1]<=nDCOffset) OR (aBufferValue[nIndex]<nDCOffset AND aBufferValue[nIndex-1]>=nDCOffset))	THEN
			IF(nCrossings=0) THEN
				nFirstZeroCrossing:=nIndex;
			END_IF
			nLastZeroCrossing:=nIndex;
			nCrossings:=nCrossings+1;
		END_IF
	END_FOR	
	
	IF nFirstZeroCrossing < nLastZeroCrossing  AND  nCrossings>2 THEN
		//interpolate zero crossings for higher accuracy
		rTimeRes:=aBufferDcTime[1]-aBufferDcTime[0]; //Buffer must contain more than 2 values
		rRange:=INT_TO_REAL(ABS(aBufferValue[nFirstZeroCrossing-1]-aBufferValue[nFirstZeroCrossing]));
		rTimeFirst:=UDINT_TO_REAL( aBufferDcTime[nFirstZeroCrossing-1])+ABS(INT_TO_REAL(aBufferValue[nFirstZeroCrossing-1])/rRange*rTimeRes);
		rRange:=INT_TO_REAL(ABS(aBufferValue[nLastZeroCrossing-1]-aBufferValue[nLastZeroCrossing]));
		rTimeLast:=UDINT_TO_REAL( aBufferDcTime[nLastZeroCrossing-1])+ABS(INT_TO_REAL(aBufferValue[nLastZeroCrossing-1])/rRange*rTimeRes);

		//Time span first to last (considering that max one time counter overflow have occured in the total time range of the time buffer)	
		IF rTimeFirst<rTimeLast THEN		
			rTimeSpan:=rTimeLast-rTimeFirst;
		ELSE //overflow of counter once (only 32bit timestamp =4.29seconds)
			rTimeSpan:=4294967296.0-rTimeFirst+rTimeLast;
		END_IF
		
		fActFrequency:=1000000000.0/rTimeSpan*(nCrossings-1)/2; //two crossings per period => Average frequency over buffer.
	ELSE
		fActFrequency:=0;
	END_IF
END_IF

]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>