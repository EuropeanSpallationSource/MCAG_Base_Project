<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.25">
  <POU Name="FB_ParseStaticCommand" Id="{c3ed2b64-9614-4a1c-8129-cc2da4522166}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ParseStaticCommand
VAR
	sVersion: STRING:='0.9.0';
END_VAR
VAR
	psCommandBufferString: POINTER TO STRING; //string to point into byte buffer		
	nEqualSign: INT;
	nQuestionMark: INT;
END_VAR

VAR_IN_OUT
	byteCommandBuffer	:	ARRAY[0..PLCPRJ_BUFFER_SIZE] OF BYTE;//ADS input buffer
	nBytesInCommandBuffer:DINT:=0;
	nIndexEndOfCommand:DINT:=0;
END_VAR

VAR_OUTPUT
	nErrorId: UDINT;
	bError:BOOL:=FALSE;
	sSubErrorString: STRING;
	sStaticCommand:STRING;
	sStaticValue:STRING;
	bReadCommand:BOOL:=FALSE;
	bWriteCommand:BOOL:=FALSE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//Parse static command	
nErrorId:=0;
bError:=FALSE;
sStaticCommand:='';
sStaticValue:='';
bReadCommand:=FALSE;
bWriteCommand:=FALSE;
psCommandBufferString:=ADR(byteCommandBuffer);

nEqualSign:=find(STR1:=PSTRING_TO_STRING(psCommandBufferString),STR2:=EPICS_WRITE_CMD);	
IF nEqualSign>0 AND nEqualSign<=nIndexEndOfCommand THEN
	bWriteCommand:=TRUE;	
	//Command
	memcpy(destAddr:=ADR(sStaticCommand[0]),srcAddr:=ADR(byteCommandBuffer[0]),n:=nEqualSign-1);
	memset(destAddr	:=ADR(sStaticCommand[nEqualSign-1]),fillByte:=0,n:=1);				//Terminate string	
	//Value
	memcpy(destAddr:=ADR(sStaticValue[0]),srcAddr:=ADR(byteCommandBuffer[nEqualSign]),n:=nIndexEndOfCommand-nEqualSign-1);
	memset(destAddr	:=ADR(sStaticValue[nIndexEndOfCommand-nEqualSign-1]),fillByte:=0,n:=1); //Terminate string
	IF NOT F_RemoveBytesFromBuffer_V1_00(nRemoveBytes:= nIndexEndOfCommand,byteCommandBuffer:=byteCommandBuffer,nBytesInCommandBuffer:=nBytesInCommandBuffer,nIndexEndOfCommand:=nIndexEndOfCommand) THEN 			
		bError:=TRUE;
		nErrorId:=301;
		sSubErrorString:='State 3: Failed to clear buffer (Parse static command write).';	
	END_IF				
ELSE
	nQuestionMark:=find(STR1:=PSTRING_TO_STRING(psCommandBufferString),STR2:=EPICS_READ_CMD);	
	IF nQuestionMark>0 AND nQuestionMark<=nIndexEndOfCommand THEN
		bReadCommand:=TRUE;	
		//Command
		memcpy(destAddr:=ADR(sStaticCommand[0]),srcAddr:=ADR(byteCommandBuffer[0]),n:=nQuestionMark-1);
		memset(destAddr	:=ADR(sStaticCommand[nQuestionMark-1]),fillByte:=0,n:=1);				//Terminate string	

		//Clean buffer
		IF NOT F_RemoveBytesFromBuffer_V1_00(nRemoveBytes:= nIndexEndOfCommand,byteCommandBuffer:=byteCommandBuffer,nBytesInCommandBuffer:=nBytesInCommandBuffer,nIndexEndOfCommand:=nIndexEndOfCommand) THEN 			
			bError:=TRUE;
			nErrorId:=302;
			sSubErrorString:='State 3: Failed to clear buffer (Parse static command read).';	
		END_IF		
	END_IF
END_IF	

IF NOT (bReadCommand OR  bWriteCommand) THEN										
	bError:=TRUE;
	nErrorId:=303;
	sSubErrorString:='State 3: Parsing of static command failed.';	
END_IF
]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>