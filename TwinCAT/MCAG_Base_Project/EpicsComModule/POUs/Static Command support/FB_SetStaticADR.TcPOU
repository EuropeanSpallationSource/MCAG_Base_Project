<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.25">
  <POU Name="FB_SetStaticADR" Id="{7fbc43b4-8e5f-4e39-87d7-003b631f1a18}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SetStaticADR
VAR
	sVersion: STRING:='0.9.0';
END_VAR
VAR_INPUT
	bEnable:BOOL:=FALSE;
	nCommadStartIndex:INT:=0;
	nCommandEndIndex:INT:=0;	
END_VAR
VAR
	psCommandBufferString: POINTER TO STRING; //string to point into byte buffer	
	bInStaticSection: BOOL;	
	sTemp: STRING;
	nEqualSign: INT;
	nComma:INT:=0;
	stemp1: STRING(255);
	sTemp2: STRING(255);
	sTemp3: STRING(255);
	sTemp4: STRING(255);
	sVal:STRING;
	nQuestionSign: INT;
	nRemove: INT;
END_VAR

VAR_IN_OUT
	byteCommandBuffer	:	ARRAY[0..PLCPRJ_BUFFER_SIZE] OF BYTE;//ADS input buffer
	nBytesInCommandBuffer:DINT:=0;
	nIndexEndOfCommand:DINT:=0;
END_VAR

VAR_OUTPUT
	nErrorId: UDINT;
	bError:BOOL:=FALSE;
	stSymInfo:SYMINFOSTRUCT;
	sSubErrorString: STRING;
END_VAR

//Should look like "ADR=idxgrp,idxoff,size,type/"   => ADR=1,2,3,4/
(*
Type:
ADST_BIT             = 33 (&H21)
ADST_INT8            = 16 (&H10)
ADST_INT16           = 2
ADST_INT32           = 3
ADST_INT64           = 20 (&H14)
ADST_UINT8           = 17 (&H11)
ADST_UINT16          = 18 (&H12)
ADST_UINT32          = 19 (&H13)
ADST_UINT64          = 21 (&H15)
ADST_REAL32          = 4
ADST_REAL64          = 5
ADST_REAL80          = 32 (&H20)
ADST_BIGTYPE         = 65 (&H41)
ADST_VOID            = 0
*)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//Static command/option 1: Check if ADR option	
IF bEnable THEN
	bError:=TRUE;
	nErrorId:=0;	
	sSubErrorString:='';	
	stSymInfo.adsDataType:=0;
	stSymInfo.byteSize:=0;
	stSymInfo.idxGroup:=0;
	stSymInfo.idxOffset:=0;
	nRemove:=0;	
	psCommandBufferString:=ADR(byteCommandBuffer);		
	nEqualSign:=find(STR1:=PSTRING_TO_STRING(psCommandBufferString),STR2:=EPICS_WRITE_CMD);															
	sTemp:=right(str:=left(str:=PSTRING_TO_STRING(psCommandBufferString),size:=nCommandEndIndex-1),SIZE:=(nCommandEndIndex)-nEqualSign-1);
	nComma:=find(STR1:=sTemp,STR2:=EPICS_ARRAY_DELIMITER);
	IF 	nComma>0 THEN
		sVal:=left(STR:=sTemp,size:=nComma);
		stSymInfo.idxGroup:=STRING_TO_UDINT(sVal);					
		sTemp1:=right(STR:=sTemp,size:=len(sTemp)-nComma);
		nRemove:=nRemove+nComma+1;		
		nComma:=find(STR1:=sTemp1,STR2:=EPICS_ARRAY_DELIMITER);
		IF 	nComma>0 THEN
			sVal:=left(STR:=sTemp1,size:=nComma);
			stSymInfo.idxOffset:=STRING_TO_UDINT(sVal);					
			sTemp2:=right(STR:=sTemp1,size:=len(sTemp1)-nComma);
			nRemove:=nRemove+nComma+1;
			nComma:=find(STR1:=sTemp2,STR2:=EPICS_ARRAY_DELIMITER); //last			
			IF 	nComma>0 THEN
				sVal:=left(STR:=sTemp2,size:=nComma);
				stSymInfo.byteSize:=STRING_TO_UDINT(sVal);					
				sTemp3:=right(STR:=sTemp2,size:=len(sTemp2)-nComma);		
				nRemove:=nRemove+nComma+1;
				nComma:=len(sTemp3);												
				IF nComma>0 AND find(str1:=stemp3,str2:=EPICS_COMMAND_OPTION_DELIMITER)=0 THEN
					stSymInfo.adsDataType:=STRING_TO_INT(sTemp3);
					nRemove:=nRemove+nComma+1;													
					bError:=FALSE;
				END_IF					
			END_IF							
		END_IF					
	END_IF
	
	IF bError THEN
		nErrorId:=310;
		bError:=TRUE;
		sSubErrorString:='State 3: Failed to parse ADR command.';	
		// try to clean buffers		
		F_RemoveBytesFromBuffer_V1_00(nRemoveBytes:= nRemove,byteCommandBuffer:=byteCommandBuffer,nBytesInCommandBuffer:=nBytesInCommandBuffer,nIndexEndOfCommand:=nIndexEndOfCommand);  		
	ELSE
		IF NOT F_RemoveBytesFromBuffer_V1_00(nRemoveBytes:= nCommandEndIndex,byteCommandBuffer:=byteCommandBuffer,nBytesInCommandBuffer:=nBytesInCommandBuffer,nIndexEndOfCommand:=nIndexEndOfCommand) THEN 			
			bError:=TRUE;
			nErrorId:=311;
			sSubErrorString:='State 3: Failed to clear buffer (ADR).';	
		END_IF	
	END_IF			
END_IF

]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>